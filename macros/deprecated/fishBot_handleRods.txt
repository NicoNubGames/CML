UNSAFE(0)

    $$<fishBot_blockPositions.txt>
    // 1: Fish 1
    // 2: Fish 2
    // 3: Logs/String/Rods
    // 4: Extra
    // 5: Water
    // 6: Crafting Table

    // Count number of rods, logs, and string
    #num_rods = 0
    #num_logs = 0
    #num_string = 0
    $$<LOOP_HOTBAR.txt>
        IF(%&inv_item% = "fishing_rod")
            #num_rods = %#num_rods% + %#inv_num%
        ENDIF

        IF(%&inv_item% = "log")
            #num_logs = %#num_logs% + %#inv_num%
        ENDIF

        IF(%&inv_item% = "string")
            #num_string = %#num_string% + %#inv_num%
        ENDIF
    NEXT

    IF(%#num_rods% = 0)
        // We need to build a pick.  Check that we have all materials

        IF(%#num_logs% = 0)
            IF(!%@paused%)
                LOG("&a[fishBot]&7 Out of logs")
                IF(%@log_to_chat%)
                    ECHO("Out of logs")
                ENDIF
            ENDIF

            @&status = "Out of logs"
            SET(@paused)
            SET(@fishbot_handle_rods)
            STOP
        ENDIF

        IF(%#num_string% < 24)
            IF(!%@paused%)
                LOG("&a[fishBot]&7 Out of string")
                IF(%@log_to_chat%)
                    ECHO("Out of string")
                ENDIF
            ENDIF

            @&status = "Out of string"
            SET(@paused)
            SET(@fishbot_handle_rods)
            STOP
        ENDIF

        // Craft 12 fishing rods from 3 logs and 24 string
        LOOKS(%#block6_yaw%,%#block6_pitch%,.2)
        WAIT(5t)

        UNSET(@open_crafting_table)
        EXEC("openCraftingTable.txt")
        DO
            WAIT(1t)
        UNTIL(@open_crafting_table)

            // Convert 3 log to planks
            UNSET(@autocraft)
            EXEC("autoCraft.txt", "autoCraft", "3 log")
            DO
                WAIT(1t)
            UNTIL(@autocraft)

            // Convert 12 planks to sticks (6 planks in each slot)
            UNSET(@autocraft)
            EXEC("autoCraft.txt", "autoCraft", "6 planks N N planks")
            DO
                WAIT(1t)
            UNTIL(@autocraft)

            // Craft 8 fishing rods
            UNSET(@autocraft)
            EXEC("autoCraft.txt", "autoCraft", "8 N N stick N stick string stick N string")
            DO
                WAIT(1t)
            UNTIL(@autocraft)

        UNSET(@close_crafting_table)
        EXEC("closeCraftingTable.txt")
        DO
            WAIT(1t)
        UNTIL(@close_crafting_table)
        
    ENDIF

    SET(@fishbot_handle_rods)

ENDUNSAFE
