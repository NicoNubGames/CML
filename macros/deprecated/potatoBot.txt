UNSAFE(0)
LOG("&a[potatoBot]&7 Start")

// Setting to talk out-loud about status changes (chests full, need materials, etc.)
SET(@log_to_chat)
UNSET(@disconnect_on_panic)
SET(@sprint_while_harvesting)

// Common elements that hook into command-status system
@&status = "Running"
@&identity = "Potato Farm Bot"
UNSET(@paused)

#yaw_north = 360
#yaw_east = 450
#yaw_south = 180
#yaw_west = 270

// Panic options
$$<initPanic.txt>
SET(allow_movement)
SET(allow_height_changes)

// Ensure first check will always say block changed
#pos_x = %XPOS% - 1
#pos_z = %ZPOS% - 1

#default_pitch = 75

DO
    // Debug code to print how many ticks each iteration takes:
    //#delta_ticks=%DAYTICKS%-%#day_ticks%
    //LOG(%#delta_ticks%)
    //#day_ticks=%DAYTICKS%

    IF(!@paused)
        // Look to default position
        LOOKS(+0,%#default_pitch%,.2)
        
        // Has our block position changed?
        IF((%#pos_x%!=%XPOS%) || (%#pos_z%!=%ZPOS%))

            // Reset movement timer
            #movement_timer = 0
        
            // Get block standing on
            GETIDREL(0,-1,0,&standing_block)
            GETIDREL(0,0,0,&occupied_block)

            // Are we standing on a cobblestone stair?
            IF((%&standing_block%="stone_stairs")||(%&occupied_block%="stone_brick_stairs"))
        
                KEYUP(forward)
            
                // The direction of the chests is marked by a crafting table
            
                GETIDREL(0,-1,-1,&north_block)
                GETIDREL(0,-1,1,&south_block)
                GETIDREL(1,-1,0,&east_block)
                GETIDREL(-1,-1,0,&west_block)
                
                IF(%&north_block% = "crafting_table")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%&east_block% = "crafting_table")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%&south_block% = "crafting_table")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%&west_block% = "crafting_table")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF
            
                WAIT(5t)

                // Now that we are looking at the chest set, we place our items appropriately
                UNSET(@potatobot_handle_chests)
                EXEC("potatoBot_handleChests.txt")
                DO
                    WAIT(1t)
                UNTIL(@potatobot_handle_chests)
            
                // Now we visually inspect the tile to decode which direction to initially walk
            
                LOOKS(+0,90,.2)
                WAIT(5t)
            
                IF(%HIT_FACING% = "north")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "east")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "south")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "west")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF
            
                KEYDOWN(forward)

            // Are we standing on birch stairs?
            ELSEIF((%&standing_block%="birch_stairs")||(%&occupied_block%="birch_stairs"))

                KEYUP(forward)

                LOOKS(+0,90,.2)
                WAIT(5t)
            
                IF(%HIT_FACING% = "north")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "east")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "south")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "west")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF

                WAIT(4t)

                KEYDOWN(forward)

            ELSEIF((%&standing_block%="farmland")||(%&occupied_block%="farmland"))

                // Look down (if we aren't already)
                LOOK(+0,%#default_pitch%)
                PICK("potato")

                IF(%HITID%="potatoes")
                    // If the plant is potatoes, and it's fully grown, harvest it
                    IF(%HIT_AGE%=7)
                        KEY(attack)
                        WAIT(1t)
                        KEY(use)
                    ENDIF

                ELSEIF(%HITID%="farmland")
                    // If the farmland is empty, plant a potato
                    KEY(use)
                ENDIF

                // If we're on farmland and block in front of us is water with no block on top, there
                // is an unprotected water block and we shouldn't continue, for fear of falling in.
                IF(%DIRECTION% = "N")
                    GETIDREL(0,0,-1,&block_ahead_floor)
                    GETIDREL(0,1,-1,&block_ahead_top)
                ELSEIF(%DIRECTION% = "E")
                    GETIDREL(1,0,0,&block_ahead_floor)
                    GETIDREL(1,1,0,&block_ahead_top)
                ELSEIF(%DIRECTION% = "S")
                    GETIDREL(0,0,1,&block_ahead_floor)
                    GETIDREL(0,0,1,&block_ahead_top)
                ELSEIF(%DIRECTION% = "W")
                    GETIDREL(-1,0,0,&block_ahead_floor)
                    GETIDREL(-1,1,0,&block_ahead_top)
                ENDIF

                IF((%&block_ahead_floor% = "water") && (%&block_ahead_top% = "air"))
                    IF(!%@paused%)
                        LOG("&a[potatoBot]&7 Unsafe water encountered")
                        IF(%@log_to_chat%)
                            ECHO("Unsafe water encountered")
                        ENDIF
                    ENDIF

                    @&status = "Unsafe water encountered"
                    SET(@paused)
                    KEYUP(forward)
                ENDIF

            ELSEIF((%&standing_block%="planks")||(%&occupied_block%="planks"))

                // If we're standing on a plank, and the block to the right is a plank,
                // turn right.  Else, if there is one on left, turn left

                IF(%DIRECTION% = "N")
                    GETIDREL(-1,-1,0,&block_left)
                    GETIDREL(1,-1,0,&block_right)

                ELSEIF(%DIRECTION% = "E")
                    GETIDREL(0,-1,-1,&block_left)
                    GETIDREL(0,-1,1,&block_right)

                ELSEIF(%DIRECTION% = "S")
                    GETIDREL(1,-1,0,&block_left)
                    GETIDREL(-1,-1,0,&block_right)

                ELSEIF(%DIRECTION% = "W")
                    GETIDREL(0,-1,1,&block_left)
                    GETIDREL(0,-1,-1,&block_right)
                ENDIF

                IF(%&block_right% = "planks")
                    LOOK(+90,%#default_pitch%)
                ELSEIF(%&block_left% = "planks")
                    LOOK(-90,%#default_pitch%)
                ENDIF

            ELSEIF((%&standing_block%="crafting_table")||(%&occupied_block%="crafting_table"))
                // If we're standing on a crafting table, turns toward the adjacent stone stair block, if one exists
                // This is able to handle the certain layout where a plank safety circuit leads the bot to the crafting table

                GETIDREL(0,-1,-1,&north_block)
                GETIDREL(0,-1,1,&south_block)
                GETIDREL(1,-1,0,&east_block)
                GETIDREL(-1,-1,0,&west_block)
            
                // Look sharply, because we are still walking forward
                IF((%&north_block% = "stone_stairs") || (%&north_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_north%,%#default_pitch%)
                ELSEIF((%&east_block% = "stone_stairs") || (%&east_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_east%,%#default_pitch%)
                ELSEIF((%&south_block% = "stone_stairs") || (%&south_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_south%,%#default_pitch%)
                ELSEIF((%&west_block% = "stone_stairs") || (%&west_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_west%,%#default_pitch%)
                ENDIF

            ENDIF

            // Only save position when we actually have executed a new block
            #pos_x = %XPOS%
            #pos_z = %ZPOS%

        ENDIF

        // If we're ever looking at an OPEN trapdoor, somebody is probably
        // playing pranks.  Close the trapdoor
        IF((%HITID% = "trapdoor") && (%HIT_OPEN%))
            KEY(use)
        ENDIF

        KEYDOWN(forward)

        IF(%@sprint_while_harvesting%)
            KEYDOWN(sprint)
        ENDIF
        
        // Increment movement timer
        INC(#movement_timer)
        
        // Has our position changed in the past 60 seconds? (excluding external scripts)
        IF(%#movement_timer%>1200)
            // Rotate 90 degrees clockwise
            LOOKS(+90,%#default_pitch%,.2)
        ENDIF

        $$<eatFood.txt>
    ELSE
        $$<pauseRotate.txt>
    ENDIF

    $$<snapYaw.txt>
    $$<checkForPanic.txt>

    WAIT(1t)
LOOP

@&status = "Idle"

ENDUNSAFE
