// Will break sand and protect durability

&iter_pick = "stone redstone_ore lit_redstone_ore lapis_ore gold_ore coal_ore iron_ore diamond_ore cobblestone stained_hardened_clay hardened_clay"
&iter_pick[] = SPLIT(" ",%&iter_pick%)
&iter_shovel = "dirt sand gravel clay"
&iter_shovel[] = SPLIT(" ",%&iter_shovel%)

$$<snapYaw.txt>

SET(prev_done)
#saved_yaw = %YAW% + 180

// Ensure first check will always say block changed
#pos_x = %XPOS% - 1
#pos_z = %ZPOS% - 1

$$<autoDig_blockPositions.txt>

// Main control loop
UNSAFE(0)
DO
    // If we're in a new block, update our block positions
    IF((%#pos_x%!=%XPOS%)||(%#pos_z%!=%ZPOS%))
        #pos_x = %XPOS%
        #pos_z = %ZPOS%
        $$<autoDig_blockPositions.txt>
    ENDIF

    // Until blocks 0-95 are air, look at them successively and attack
    SET(done)
    FOR(#i,0,95)
        GETID(%#block_x[%#i%]%,%#block_y[%#i%]%,%#block_z[%#i%]%,&id)
        IF(%&id% != "air")
            LOOK(%#block_yaw[%#i%]%,%#block_pitch[%#i%]%)
            UNSET(done)
            BREAK
        ENDIF
    NEXT

    // Pick whichever tool is most effective
    is_pick_block = false
    FOREACH(&iter_pick,&block)
        IF(%HITID% == %&block%)
            is_pick_block = true
            BREAK
        ENDIF
    NEXT

    is_shovel_block = false
    FOREACH(&iter_shovel,&block)
        IF(%HITID% == %&block%)
            is_shovel_block = true
            BREAK
        ENDIF
    NEXT

    IF((%is_pick_block%) && (%ITEM% != "diamond_pickaxe"))
        PICK("diamond_pickaxe")
        WAIT(1t)
    ENDIF

    IF((%is_shovel_block%) && (%ITEM% != "diamond_shovel"))
        PICK("diamond_shovel")
        WAIT(1t)
    ENDIF

    // Stop when durability gets low, for safety
    IF(((%ITEM% == "diamond_pickaxe") || (%ITEM% == "diamond_shovel")) && (%DURABILITY% <= 50))
        KEYUP(forward)
        LOG("Ran out of durability of tool")
        STOP
    ENDIF

    // If we are just beginning a break-round, save our yaw for later
    IF(prev_done && done)
        #saved_yaw = %YAW% + 180
    ENDIF

    SPRINT

    // If we're done with this layer, walk forward until we're not done
    IF(done)
        // If we're done, and the block in front of us is air, attempt to make a block in front of us (bridge)
        IF(%DIRECTION% = "N")
            #block_x[100] = (%XPOS% + 0)
            #block_y[100] = (%YPOS% - 1)
            #block_z[100] = (%ZPOS% - 1)
            #block_yaw[100] = 358
            #block_pitch[100] = 59
        ELSEIF(%DIRECTION% = "E")
            #block_x[100] = (%XPOS% + 1)
            #block_y[100] = (%YPOS% - 1)
            #block_z[100] = (%ZPOS% + 0)
            #block_yaw[100] = 448
            #block_pitch[100] = 59
        ELSEIF(%DIRECTION% = "S")
            #block_x[100] = (%XPOS% + 0)
            #block_y[100] = (%YPOS% - 1)
            #block_z[100] = (%ZPOS% + 1)
            #block_yaw[100] = 538
            #block_pitch[100] = 59
        ELSEIF(%DIRECTION% = "W")
            #block_x[100] = (%XPOS% - 1)
            #block_y[100] = (%YPOS% - 1)
            #block_z[100] = (%ZPOS% + 0)
            #block_yaw[100] = 268
            #block_pitch[100] = 59
        ENDIF

        GETID(%#block_x[100]%,%#block_y[100]%,%#block_z[100]%,&id)

        IF(%&id% == "air")
            LOOK(%#saved_yaw%,8)
            PICK("cobblestone")
            KEYDOWN(sneak)
            KEYDOWN(forward)
            WAIT(20t)
            KEYUP(forward)
            KEYDOWN(back)
            WAIT(2t)
            KEYUP(back)
            KEYUP(sneak)
            WAIT(1t)
            LOOK(+180,83)
            WAIT(1t)
            KEY(use)
            WAIT(1t)
            LOOK(%#saved_yaw%,8)
            WAIT(1t)
        ENDIF

        // Look in our original direction
        LOOK(%#saved_yaw%,+0)
        KEYDOWN(forward)
    ELSE
        KEY(attack)
        KEYUP(forward)
    ENDIF

    prev_done = done

    $$<eatFood.txt>
    $$<centerSelf.txt>

    WAIT(1t)
LOOP
ENDUNSAFE