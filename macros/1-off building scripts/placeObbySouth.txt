// Will break ice if encountered, and place 3 obby

&iter_pick = "stone redstone_ore lit_redstone_ore lapis_ore gold_ore coal_ore iron_ore diamond_ore cobblestone packed_ice"
&iter_pick[] = SPLIT(" ",%&iter_pick%)

&iter_shovel = "dirt sand gravel clay"
&iter_shovel[] = SPLIT(" ",%&iter_shovel%)

UNSET(@reinforced)

$$<snapYaw.txt>

ECHO("/cto")
PICK("stone")
WAIT(5t)
IF(%ITEM% == "stone")
	ECHO("/ctf IceRhode")
	SET(@reinforced)
ELSE
	LOG("Ran out of stone")
ENDIF
WAIT(5t)
ECHO("/ctb")

$$<placeObbySouth_blockPositions.txt>

// Identify whether we're moving forward-left or forward-right, depending on pattern of ice blocks
GETID(%#block_x[6]%,%#block_y[6]%,%#block_z[6]%,&left_id)
GETID(%#block_x[7]%,%#block_y[7]%,%#block_z[7]%,&right_id)

IF(%&left_id% != "packed_ice")
    SET(move_left)
ELSEIF(%&right_id% != "packed_ice")
    UNSET(move_left)
ELSE
    LOG("Not facing ice tunnel diagonal wall")
ENDIF

#init_yaw = %YAW% + 180

UNSAFE(0)
DO
	$$<placeObbySouth_blockPositions.txt>

	IF(!%@reinforced%)
		KEYUP(forward)
		KEYUP(right)
		LOG("Ran out of stone")
		STOP
	ENDIF

	// Until blocks 0-2 are all obby, look at them successively and place
	// If we're handling first block, switch to "attack mode" if there is a block there
	SET(done)
	FOR(#i,0,2)
		GETID(%#block_x[%#i%]%,%#block_y[%#i%]%,%#block_z[%#i%]%,&id)
		IF((%&id% == "air") || (%&id% == "packed_ice"))
			LOOK(%#block_yaw[%#i%]%,%#block_pitch[%#i%]%)
			UNSET(done)
			BREAK
		ENDIF
	NEXT

	UNSET(attack)
	IF((%#i% == 0) && (%&id% == "packed_ice"))
		SET(attack)
	ENDIF

	KEYDOWN(sneak)

	// If we're attacking, get the pick
	IF(attack)
		PICK("diamond_pickaxe")
		WAIT(1t)
	ENDIF

    // Stop when durability gets low, for safety
    IF(((%ITEM% == "diamond_pickaxe") || (%ITEM% == "diamond_shovel")) && (%DURABILITY% <= 50))
        KEYUP(forward)
        LOG("Ran out of durability of tool")
        STOP
    ENDIF

	// If we're done with this layer, walk forward until we're not done
	IF(done)
		LOOK(%#init_yaw%,+0)
		KEYDOWN(forward)
        IF(move_left)
            KEYDOWN(left)
        ELSE
            KEYDOWN(right)
        ENDIF
	ELSE
		KEYUP(forward)
        IF(move_left)
            KEYUP(left)
        ELSE
            KEYUP(right)
        ENDIF

		IF(attack)
			KEY(attack)
			WAIT(2t)
		ELSE
			DO(20)
				PICK("obsidian")
				WAIT(1t)
			UNTIL(%ITEM% == "obsidian")

			IF(%ITEM% != "obsidian")
				LOG(%ITEM%)
				KEYUP(forward)
		        IF(move_left)
		            KEYUP(left)
		        ELSE
		            KEYUP(right)
		        ENDIF
				LOG("Ran out of obsidian")
				STOP
			ENDIF

			IF(%ITEM% == "obsidian")
				KEY(use)
			ENDIF
		ENDIF
	ENDIF

	$$<eatFood.txt>
	$$<snapYaw.txt>

	WAIT(1t)
LOOP
ENDUNSAFE