// Will dig a 3-high tunnel, and stop if lava or water is encountered in adjacent blocks

&iter_pick = "stone redstone_ore lit_redstone_ore lapis_ore gold_ore coal_ore iron_ore diamond_ore cobblestone"
&iter_pick[] = SPLIT(" ",%&iter_pick%)

&iter_shovel = "dirt sand gravel clay"
&iter_shovel[] = SPLIT(" ",%&iter_shovel%)

UNSAFE(0)
DO
	$$<tunnelDig_blockPositions.txt>

	// First, check if any blocks 0-13 are water or lava
	FOR(#i,0,13)
		GETID(%#block_x[%#i%]%,%#block_y[%#i%]%,%#block_z[%#i%]%,&id)

		IF((%&id%=="water")||(%&id%=="lava")||(%&id%=="flowing_lava"))
			KEYUP(forward)
			LOG("Water/lava detected")
			STOP
		ENDIF
	NEXT

	// Pick whichever tool is most effective
    is_pick_block = false
	FOREACH(&iter_pick,&block)
		IF(%HITID% == %&block%)
			is_pick_block = true
			BREAK
		ENDIF
	NEXT

	is_shovel_block = false
	FOREACH(&iter_shovel,&block)
		IF(%HITID% == %&block%)
			is_shovel_block = true
			BREAK
		ENDIF
	NEXT

	IF((%is_pick_block%) && (%ITEM% != "diamond_pickaxe"))
		PICK("diamond_pickaxe")
		WAIT(1t)
	ENDIF

	IF((%is_shovel_block%) && (%ITEM% != "diamond_shovel"))
		PICK("diamond_shovel")
		WAIT(1t)
	ENDIF

	// Stop when durability gets low, for safety
    IF(((%ITEM% == "diamond_pickaxe") || (%ITEM% == "diamond_shovel")) && (%DURABILITY% <= 50))
		KEYUP(forward)
		LOG("Ran out of durability of tool")
		STOP
    ENDIF

	// Until blocks 0-2 are air, look at them successively and attack
	SET(done)
	FOR(#i,0,2)
		GETID(%#block_x[%#i%]%,%#block_y[%#i%]%,%#block_z[%#i%]%,&id)
		IF(%&id% != "air")
			LOOK(%#block_yaw[%#i%]%,%#block_pitch[%#i%]%)
			UNSET(done)
			BREAK
		ENDIF
	NEXT

	// If block 13 (one immediately in front) is not air, attack it
	GETID(%#block_x[13]%,%#block_y[13]%,%#block_z[13]%,&id)
	IF(%&id% != "air")
		LOOK(%#block_yaw[13]%,%#block_pitch[13]%)
		UNSET(done)
	ENDIF

	// If we're done with this layer, walk forward until we're not done
	IF(done)
		KEYDOWN(forward)
	ELSE
		KEY(attack)
		KEYUP(forward)
	ENDIF

	SPRINT

	$$<eatFood.txt>
	$$<snapYaw.txt>
	$$<centerSelf.txt>
	WAIT(1t)
LOOP
ENDUNSAFE