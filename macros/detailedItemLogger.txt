// Will log items of 10 individual chests the script is facing into "mats.txt" in the following form:
// chest_id, chest_x_pos, chest_y_pos, chest_z_pos, amount

// This is an extension of "matsLogger" that was for a more limited use-case

#correct_xpos = 934
#correct_zpos = -1952

$$<detailedItemLogger_blockPositions.txt>

IF((%XPOS% != %#correct_xpos%) || (%ZPOS% != %#correct_zpos%))
	LOG("&a[mats]&7 Not in correct spot")
	STOP
ENDIF

#saved_yaw = %YAW% + 180
#saved_pitch = %PITCH%

LOGTO(mats.txt,[mats] %DATETIME%)

UNSAFE(0)
ARRAYSIZE(#block_x,#num_blocks)
DEC(#num_blocks)
FOR(#i,0,%#num_blocks%)
    LOOK(%#block_yaw[%#i%]%,%#block_pitch[%#i%]%)
    WAIT(1t)
    DO(20)
    	KEY(use)
    	WAIT(1t)
    UNTIL(%GUI% == "GUICHEST")
    IF(%GUI% != "GUICHEST")
    	LOG("&a[mats]&7 Chest did not open")
    	STOP
    ENDIF

    // Initialize array for mats and amounts for current chest
    FOR(#j,0,53)
        &mat_names[%#j%] = "air"
        #mat_counts[%#j%] = 0
    NEXT

    // Loop through chest and add to mats array as needed
    FOR(#j,0,53)
    	GETSLOTITEM(%#j%,&item,#size)

    	IF(%&item% != "air")
            // Find index of this material for this chest, if it exists.
            // If it doesn't exist, add it to the array
            UNSET(found)
            FOR(#k,0,53)
                IF((%&item% == %&mat_names[%#k%]%))
                    SET(found)
                    BREAK
                ELSEIF(%&mat_names[%#k%]% == "air")
                    BREAK
                ENDIF
            NEXT

            IF(!found)
                &mat_names[%#k%] = %&item%
            ENDIF

            #mat_counts[%#k%] = %#mat_counts[%#k%]% + %#size%
    	ENDIF
    NEXT

    // Log all of found items in this chest to mats.txt
    FOR(#j,0,53)
        IF(%&mat_names[%#j%]% != "air")
            #chest_id = %#i% + 1
            LOGTO(mats.txt,%#chest_id% %#block_x[%#i%]% %#block_y[%#i%]% %#block_z[%#i%]% %&mat_names[%#j%]% %#mat_counts[%#j%]%)
        ELSE
            BREAK
        ENDIF
    NEXT

    DO(20)
    	GUI
    	WAIT(1t)
    UNTIL(%GUI% = "NONE")

    IF(%GUI% != "NONE")
    	LOG("&a[mats]&7 Chest did not close")
    	STOP
    ENDIF

    WAIT(3t)
NEXT

DO(20)
	GUI
	WAIT(1t)
UNTIL(%GUI% = "NONE")

IF(%GUI% != "NONE")
	LOG("&a[mats]&7 Last chest did not close")
	STOP
ENDIF

LOOK(%#saved_yaw%,%#saved_pitch%)
ENDUNSAFE