UNSAFE(0)
LOG("&a[digBot]&7 Start")

// Setting to talk out-loud about status changes (chests full, need materials, etc.)
SET(@log_to_chat)
SET(@disconnect_on_panic)

// Common elements that hook into command-status system
@&status = "Running"
@&identity = "Digging Bot"
UNSET(@paused)

#yaw_north = 360
#yaw_east = 450
#yaw_south = 180
#yaw_west = 270

// Panic options
$$<initPanic.txt>
SET(movement_allowed)
SET(allow_height_changes)

// Ensure first check will always say block changed
#pos_x = %XPOS% - 1
#pos_z = %ZPOS% - 1

#default_pitch = 50

// This bot is used for bulk digging projects, such as strip mining or "vault" digging.  Starting at some base y-level, it will
// build a cobblestone 1x1 tower upwards into any ceiling it finds, until #max_height is reached.  It will then carefully make its way
// down, cutting out a 7x7 cube centered on the block it started ascending at.

// When it reaches its initial height again (the base height), if that block is a cobblestone stair, it will move in the direction
// until another cobblestone stair block is potentially reached.  Once that block is reached, the bot will repeat the cube cutting
// process.

// On the upwards part, if any block above the bot in a 5x5 (by 3 height) is water or lava, it will log the event and abandon
// the current cube cutting operation, and continue to the next one.

DO
    IF(!@paused)
        // Look to default position
        LOOKS(+0,%#default_pitch%,.2)
        
        // Has our block position changed?
        IF((%#pos_x%!=%XPOS%)||(%#pos_z%!=%ZPOS%))
        
            // Reset movement timer
            #movement_timer = 0
        
            // Get block standing on
            GETIDREL(0,-1,0,&standing_block)
        
            // Are we standing on cobblestone or stone brick stairs?
            IF((%&standing_block% = "stone_stairs")||(%&standing_block% = "stone_brick_stairs"))
        
                KEYUP(forward)
            
                // If this is a "home position", the direction of the adjacent crafting table will mark
                // the direction of the chests
            
                GETIDREL(0,-1,-1,&north_block)
                GETIDREL(0,-1,1,&south_block)
                GETIDREL(1,-1,0,&east_block)
                GETIDREL(-1,-1,0,&west_block)

                UNSET(home)
            
                IF(%&north_block% = "crafting_table")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
                    SET(home)
                ELSEIF(%&east_block% = "crafting_table")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
                    SET(home)
                ELSEIF(%&south_block% = "crafting_table")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
                    SET(home)
                ELSEIF(%&west_block% = "crafting_table")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                    SET(home)
                ENDIF
                
                // If there is an adjacent crafting table, handle the chests
                IF(home)
                    WAIT(5t)

                    // Handle inventory management
                    UNSET(@birchbot_handle_chests)
                    EXEC("birchBot_handleChests.txt")
                    DO
                        WAIT(1t)
                    UNTIL(@birchbot_handle_chests)

                    // Build 4 axes, if we need them
                    UNSET(@birchbot_handle_axes)
                    EXEC("birchBot_handleAxes.txt")
                    DO
                        WAIT(1t)
                    UNTIL(@birchbot_handle_axes)
                ENDIF
            
                // Either way, we visually inspect the tile to decode which direction to initially walk
            
                LOOKS(+0,90,.2)
                WAIT(5t)
            
                IF(%HIT_FACING% = "north")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "east")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "south")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "west")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF
            
                KEYDOWN(forward)
        
            // Are we standing on grass or dirt?
            ELSEIF((%&standing_block%="grass")||(%&standing_block%="dirt"))

                // Destroy top of tree, if any exists

                UNSET(@treebot_treetop)
                EXEC("birchBot_treeTop.txt")
                DO
                    WAIT(1t)
                UNTIL(@treebot_treetop)

                // We look in the four cardinal directions for floor log
                IF(%DIRECTION% = "N")
                    GETIDREL(0,-1,1,&block_behind)
                ELSEIF(%DIRECTION% = "E")
                    GETIDREL(-1,-1,0,&block_behind)
                ELSEIF(%DIRECTION% = "S")
                    GETIDREL(0,-1,-1,&block_behind)
                ELSEIF(%DIRECTION% = "W")
                    GETIDREL(1,-1,0,&block_behind)
                ENDIF

                IF(%&block_behind% = "birch_stairs")

                    IF(%DIRECTION% = "N")
                        GETIDREL(-1,-1,0,&block_left)
                        GETIDREL(1,-1,0,&block_right)

                    ELSEIF(%DIRECTION% = "E")
                        GETIDREL(0,-1,-1,&block_left)
                        GETIDREL(0,-1,1,&block_right)

                    ELSEIF(%DIRECTION% = "S")
                        GETIDREL(1,-1,0,&block_left)
                        GETIDREL(-1,-1,0,&block_right)

                    ELSEIF(%DIRECTION% = "W")
                        GETIDREL(0,-1,1,&block_left)
                        GETIDREL(0,-1,-1,&block_right)
                    ENDIF

                    IF(%&block_left% = "log")
                        KEYUP(forward)
                        LOOKS(-90,%#default_pitch%,.2)
                        WAIT(3t)

                    ELSEIF(%&block_right% = "log")
                        KEYUP(forward)
                        LOOKS(+90,%#default_pitch%,.2)
                        WAIT(3t)
                    ENDIF

                    KEYDOWN(forward)

                ENDIF
        
            // Are we standing on birch stairs?
            ELSEIF(%&standing_block%="birch_stairs")
        
                // Look towards birch stairs direction

                KEYUP(forward)
                
                LOOKS(+0,90,.2)
                WAIT(5t)
            
                IF(%HIT_FACING% = "north")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "east")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "south")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "west")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF

                // Destroy front of tree, if any exists, and plant saplings
        
                UNSET(@treebot_treefront)
                EXEC("birchBot_treeFront.txt")
                DO
                    WAIT(1t)
                UNTIL(@treebot_treefront)

            // Are we standing on a plank?
            ELSEIF(%&standing_block% = "planks")
                // If we're standing on a plank, and the block to the right is a plank,
                // turn right.  Else, if there is one on left, turn left

                IF(%DIRECTION% = "N")
                    GETIDREL(-1,-1,0,&block_left)
                    GETIDREL(1,-1,0,&block_right)

                ELSEIF(%DIRECTION% = "E")
                    GETIDREL(0,-1,-1,&block_left)
                    GETIDREL(0,-1,1,&block_right)

                ELSEIF(%DIRECTION% = "S")
                    GETIDREL(1,-1,0,&block_left)
                    GETIDREL(-1,-1,0,&block_right)

                ELSEIF(%DIRECTION% = "W")
                    GETIDREL(0,-1,1,&block_left)
                    GETIDREL(0,-1,-1,&block_right)
                ENDIF

                IF(%&block_right% = "planks")
                    LOOK(+90,%#default_pitch%)
                ELSEIF(%&block_left% = "planks")
                    LOOK(-90,%#default_pitch%)
                ENDIF

            // Are we standing on a crafting table?
            ELSEIF(%&standing_block% = "crafting_table")
                // If we're standing on a crafting table, turns toward the adjacent stone stair block, if one exists
                // This is able to handle the certain layout where a plank safety circuit leads the bot to the crafting table

                GETIDREL(0,-1,-1,&north_block)
                GETIDREL(0,-1,1,&south_block)
                GETIDREL(1,-1,0,&east_block)
                GETIDREL(-1,-1,0,&west_block)
            
                // Look sharply, because we are still walking forward
                IF((%&north_block% = "stone_stairs") || (%&north_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_north%,%#default_pitch%)
                ELSEIF((%&east_block% = "stone_stairs") || (%&east_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_east%,%#default_pitch%)
                ELSEIF((%&south_block% = "stone_stairs") || (%&south_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_south%,%#default_pitch%)
                ELSEIF((%&west_block% = "stone_stairs") || (%&west_block% = "stone_brick_stairs"))
                    LOOK(%#yaw_west%,%#default_pitch%)
                ENDIF

            ENDIF

            // Only save position when we actually have executed a new block
            #pos_x = %XPOS%
            #pos_z = %ZPOS%

        ENDIF

        // Walk
        KEYDOWN(forward)
        
        // Increment movement timer
        INC(#movement_timer)
        
        // Has our position changed in the past 60 seconds? (excluding external scripts)
        IF(%#movement_timer%>1200)
            // Rotate 90 degrees clockwise
            LOOKS(+90,%#default_pitch%,.2)
        ENDIF

        $$<eatFood.txt>

    ELSE
        // If paused, we rotate pitch so it's obvious
        KEYUP(forward)

        #pause_rotate_pitch = %#pause_rotate_pitch% + 2

        IF(%#pause_rotate_pitch% >= 90)
            #pause_rotate_pitch = 0
        ELSEIF(%#pause_rotate_pitch% < 0)
            #pause_rotate_pitch = 0
        ENDIF

        LOOK(+0,%#pause_rotate_pitch%)
    ENDIF

    $$<snapYaw.txt>
    $$<checkForPanic.txt>

    WAIT(1t)

LOOP

ENDUNSAFE