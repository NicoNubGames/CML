// Will log items of 10 individual chests the script is facing into "mats.txt" in the following form:
// chest_id chest_x_pos chest_y_pos chest_z_pos amount

// Customize "blockPositions" with coordinate parser utility to change the default chest locations

UNSAFE(0)
$$<itemLogger_blockPositions.txt>

#saved_yaw = %CARDINALYAW%
#saved_pitch = %PITCH%

$$<CML_snapYaw.txt>

LOGTO(mats.txt,[item] %DATETIME%)

FOR(#i,0,%#block_end%)
    LOOK(%#block_yaw[%#i%]%,%#block_pitch[%#i%]%)

    // Open chest
    $$<CML_open.txt>
    IF(%&warn%)
        LOG("&a[item]&7 %&warn%")
    ENDIF

    // Initialize array for mats and amounts for current chest
    FOR(#j,0,53)
        &mat_names[%#j%] = "air"
        #mat_counts[%#j%] = 0
    NEXT

    // Loop through chest and add to mats array as needed
    FOR(#j,0,53)
        GETSLOTITEM(%#j%,&item,#size)

        IF(%&item% != "air")
            // Find index of this material for this chest, if it exists.
            // If it doesn't exist, add it to the array
            UNSET(found)
            FOR(#k,0,53)
                IF((%&item% == %&mat_names[%#k%]%))
                    SET(found)
                    BREAK
                ELSEIF(%&mat_names[%#k%]% == "air")
                    BREAK
                ENDIF
            NEXT

            IF(!found)
                &mat_names[%#k%] = %&item%
            ENDIF

            #mat_counts[%#k%] = %#mat_counts[%#k%]% + %#size%
        ENDIF
    NEXT

    // Log all of found items in this chest to mats.txt
    FOR(#j,0,53)
        IF(%&mat_names[%#j%]% != "air")
            #chest_id = %#i% + 1
            LOGTO(mats.txt,%#chest_id% %#block_x[%#i%]% %#block_y[%#i%]% %#block_z[%#i%]% %&mat_names[%#j%]% %#mat_counts[%#j%]%)
        ELSE
            BREAK
        ENDIF
    NEXT

    // Close chest
    $$<CML_close.txt>
    IF(%&warn%)
        LOG("&a[item]&7 %&warn%")
    ENDIF
NEXT

LOOK(%#saved_yaw%,%#saved_pitch%)
ENDUNSAFE
