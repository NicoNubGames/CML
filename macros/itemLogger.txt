// Will log items of 10 individual chests the script is facing into "mats.txt" in the following form:
// chest_id chest_x_pos chest_y_pos chest_z_pos amount

// Customize "blockPositions" with coordinate parser utility to change the default chest locations

UNSAFE(0)
$$<itemLogger_blockPositions.txt>

#saved_yaw = %CARDINALYAW%
#saved_pitch = %PITCH%

#mat_length = 0

// Look at each chest and open, then go through each item.
// If an item is not in the mats list, add it.  Else, if it is, add its number to that entry
FOR(#i,0,%#block_end%)
    LOOK(%#block_yaw[%#i%]%,%#block_pitch[%#i%]%)

    // Open chest
    $$<CML_open.txt>
    IF(%&warn%)
        LOG("&a[item]&7 %&warn%")
    ENDIF

    // Loop through chest and add to mats array as needed
    FOR(#j,0,53)
        GETSLOTITEM(%#j%,&item,#size)

        IF(%&item% != "air")
            // Find index of this material for this chest, if it exists
            // If it doesn't exist, add it to the array
            UNSET(found)
            FOR(#k,0,%#mat_length%)
                IF(%#k% == %#mat_length%)
                    BREAK
                ENDIF

                IF(%&item% == %&mat_names[%#k%]%)
                    SET(found)
                    BREAK
                ENDIF
            NEXT

            IF(!found)
                &mat_names[%#k%] = %&item%
                #mat_counts[%#k%] = 0
                INC(#mat_length)
            ENDIF

            #mat_counts[%#k%] = %#mat_counts[%#k%]% + %#size%
        ENDIF
    NEXT

    // Close chest
    $$<CML_close.txt>
    IF(%&warn%)
        LOG("&a[item]&7 %&warn%")
    ENDIF
NEXT

// Log all found items, in order of finding them, to the mats.txt file in space-delimited form
FOR(#i,0,%#mat_length%)
    IF(%#i% == %#mat_length%)
        BREAK
    ENDIF

    IF(%#i% == 0)
        &data = "%#mat_counts[%#i%]%"
    ELSE
        &data = "%&data% %#mat_counts[%#i%]%"
    ENDIF
NEXT

LOGTO(mats.txt,%&data%)

LOOK(%#saved_yaw%,%#saved_pitch%)
ENDUNSAFE
