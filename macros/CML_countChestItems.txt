// Given &items, builds an array #counts[] composed of the sum amounts of each type of item, and
// #total representing total amount

// Split given string into array
&cml_items[] = SPLIT(" ",%&items%)

// Build resulting numerical array
#cml_i = 0
FOREACH(&cml_items,&cml_item)
	#counts[%#cml_i%] = 0
	INC(#cml_i)
NEXT

IF((%GUI% == "GUICHEST") && (%CONTAINERSLOTS% == 63))
	#chest_begin = 0
	#chest_end = 26
	#inv_begin = 27
	#inv_end = 62
ELSEIF((%GUI% == "GUICHEST") && (%CONTAINERSLOTS% == 90))
	#chest_begin = 0
	#chest_end = 53
	#inv_begin = 54
	#inv_end = 89
ENDIF

// Loop through chest.
#cml_j = 0
#total = 0
FOR(#cml_i,%#chest_begin%,%#chest_end%)
	GETSLOTITEM(%#cml_i%,&cml_id,#cml_size,#cml_dura)

	// If id matches with item, add amount to sum for that entry
	#cml_j = 0
	FOREACH(&cml_items,&cml_item)
		IF(%&cml_id% == %&cml_item%)
			#counts[%#cml_j%] = %#counts[%#cml_j%]% + %#cml_size%
			#total = %#total% + %#cml_size%
			BREAK
		ENDIF

		INC(#cml_j)
	NEXT
NEXT