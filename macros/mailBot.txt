// The idea of this bot is to facilitate automated movement of ingredients between various farms (for EXP production, assumedly)
// The bot supports some generic features for moving arbitrary items from one place to another, embedded on a transportation graph





UNSAFE(0)
LOG("&a[mailBot]&7 Start")

// Setting to talk out-loud about status changes (chests full, need materials, etc.)
UNSET(@log_to_chat)
UNSET(@disconnect_on_panic)
UNSET(@disable_panic)
SET(@verbose_chat_log)

// Mailbot specific parameters
@#num_food_to_keep = 64

// Common elements that hook into command-status system
@&status = "Running"
@&identity = "Mail Bot"
UNSET(@paused)

#yaw_north = 360
#yaw_east = 450
#yaw_south = 180
#yaw_west = 270

// Panic options
$$<initPanic.txt>
SET(allow_movement)
SET(allow_height_changes)

// Ensure first check will always say block changed
#pos_x = %XPOS% - 1
#pos_z = %ZPOS% - 1

// Pitch to look towards for aesthetic effect (when it doesn't matter where we look)
#default_pitch = 50

UNSET(@mailbot_handlecommands)
EXEC("mailbot_handleCommands.txt")
DO
    WAIT(1t)
UNTIL(@mailbot_handlecommands)

DO
    // Walk
    KEYDOWN(forward)
    
    // Look to default pitch
    LOOK(+0,%#default_pitch%)
    
    // Has our block position changed?
    IF((%#pos_x%!=%XPOS%)||(%#pos_z%!=%ZPOS%))
    
        // Reset movement timer
        #movement_timer = 0
    
        // Get block standing on
        GETIDREL(0,-1,0,&standing_block)
    
        // Are we standing on an wooden trapdoor?
        IF(%&standing_block%="trapdoor")
    
            KEYUP(forward)
        
            // The direction of the chest matrix is encoded by an adjacent stone slab.  We must
            // check each of the cardinal directions.
        
            GETIDREL(0,-1,-1,&north_block)
            GETIDREL(0,-1,1,&south_block)
            GETIDREL(1,-1,0,&east_block)
            GETIDREL(-1,-1,0,&west_block)
        
            IF(%&north_block% = "stone_slab")
                LOOKS(%#yaw_north%,%#default_pitch%,.2)
        
            ELSEIF(%&east_block% = "stone_slab")
                LOOKS(%#yaw_east%,%#default_pitch%,.2)
        
            ELSEIF(%&south_block% = "stone_slab")
                LOOKS(%#yaw_south%,%#default_pitch%,.2)
        
            ELSEIF(%&west_block% = "stone_slab")
                LOOKS(%#yaw_west%,%#default_pitch%,.2)
            ENDIF
        
            WAIT(5t)
        
            // Now that we are looking at the chest matrix, we collect all of our inventory into 4 double chests
        
            UNSET(@walk_handle_chests)
            EXEC("walkBot_handleChests.txt")
            DO
                WAIT(1t)
            UNTIL(@walk_handle_chests)
        
            // Now we visually inspect the tile to decode which direction to initially walk
        
            LOOKS(+0,90,.2)
            WAIT(5t)
        
            IF(%HIT_FACING% = "north")
                LOOKS(%#yaw_north%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "east")
                LOOKS(%#yaw_east%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "south")
                LOOKS(%#yaw_south%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "west")
                LOOKS(%#yaw_west%,%#default_pitch%,.2)
            ENDIF
        
            KEYDOWN(forward)

        // Are we standing on birch stairs?
        ELSEIF(%&standing_block%="birch_stairs")

            KEYUP(forward)

            LOOKS(+0,90,.2)
            WAIT(5t)
        
            IF(%HIT_FACING% = "north")
                LOOKS(%#yaw_north%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "east")
                LOOKS(%#yaw_east%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "south")
                LOOKS(%#yaw_south%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "west")
                LOOKS(%#yaw_west%,%#default_pitch%,.2)
            ENDIF

            WAIT(4t)

            KEYDOWN(forward)
    
        ENDIF

        // Only save position when we actually have executed a new block
        #pos_x = %XPOS%
        #pos_z = %ZPOS%

    ENDIF
    
    
    // Increment movement timer
    INC(#movement_timer)
    
    // Has our position changed in the past 60 seconds? (excluding external scripts)
    IF(%#movement_timer%>1200)
        // Rotate 90 degrees clockwise
        LOOKS(+90,%#default_pitch%,.2)
    ENDIF


    $$<"eatFood.txt">

    // Snap yaw
    IF(%DIRECTION% = "N")
        LOOK(%#yaw_north%,+0)
    ELSEIF(%DIRECTION% = "E")
        LOOK(%#yaw_east%,+0)
    ELSEIF(%DIRECTION% = "S")
        LOOK(%#yaw_south%,+0)
    ELSEIF(%DIRECTION% = "W")
        LOOK(%#yaw_west%,+0)
    ENDIF

    WAIT(1t)

LOOP

ENDUNSAFE()
    


