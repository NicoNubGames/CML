UNSET(panic)

#panic_delta_x = %XPOS% - %#panic_prev_x%
#panic_delta_y = %YPOS% - %#panic_prev_y%
#panic_delta_z = %ZPOS% - %#panic_prev_z%

// This isn't an end-condition, but it's convenient to check it here.  If the user is pressing the
// grave key, they probably want us to stop.
IF(%KEY_GRAVE%)
	STOP
ENDIF

// If our health is below 5, panic
IF(%HEALTH% <= 5)
	// LOG("Low health")
	SET(panic)
ENDIF

// If our height changes, we probably fell into a hole.
// Note: We also check if we are standing on a chest/farmland.  If we are, and the difference is only 1, it's probably
// just a small divot due to the block not being high enough
IF(!allow_height_changes)
	GETIDREL(0,0,0,&panic_block)
	IF(%YPOS% != %#panic_initial_y%)
		IF((%&panic_block% != "chest") && (%&panic_block% != "trapped_chest") && (%&panic_block% != "farmland"))
			// LOG("Fell into hole")
			SET(panic)
		ELSEIF(%YPOS% != (%#panic_initial_y% - 1))
			// LOG("Y-height does not match original height")
			SET(panic)
		ENDIF
	ENDIF
ENDIF

// If we move more than 5 blocks instantaneously, assume we teleported
IF((%#panic_delta_x% > 5) || (%#panic_delta_x < -5) || (%#panic_delta_y% > 5) || (%#panic_delta_y < -5) || (%#panic_delta_z% > 5) || (%#panic_delta_z < -5))
	// LOG("Teleported")
	SET(panic)
ENDIF

// If we aren't allowed to move, check if we moved
IF(!allow_movement)
	IF((%XPOS% != %#panic_initial_x%) || (%YPOS% != %#panic_initial_y%) || (%ZPOS% != %#panic_initial_z%))
		// LOG("Moved position while movement was disallowed")
		SET(panic)
	ENDIF
ENDIF


IF(panic)
	// Only take action if we're going to disconnect, or we're not currently paused (So we don't spam if in paused panic state)
	IF(@disconnect_on_panic || !@paused)
		LOG("&a[CML]&7 Panic. Cannot recover.")
		IF(%@log_to_chat%)
			ECHO("Panic. Cannot recover.")
		ENDIF

		IF(%@disconnect_on_panic%)
			WAIT(2t)
			DISCONNECT
		ELSE
			SET(@paused)
		ENDIF
	ENDIF
ENDIF

#panic_prev_x = %XPOS%
#panic_prev_y = %YPOS%
#panic_prev_z = %ZPOS%